#@TYPE: Machine
#@NAME: fri2

#@WEBTITLE: Intel Atom E640T Processor with Intel EG20T Controller Hub Development Kit (Queens Bay) with Open Source VESA Graphics for Fish River Island II systems

#@DESCRIPTION: Machine configuration for Fish River Island II systems, without Intel-proprietary graphics bits

require conf/machine/include/tune-atom.inc
require conf/machine/include/ia32-base.inc

MACHINE_FEATURES += "wifi 3g pcbios efi"
MACHINE_EXTRA_RRECOMMENDS += "linux-firmware-iwlwifi-6000g2a-5"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
PREFERRED_VERSION_linux-yocto = "3.8%"

XSERVER ?= "${XSERVER_IA32_BASE} \
           ${XSERVER_IA32_EXT} \
           ${XSERVER_IA32_VESA} \
           "

# Syslinux does not know about the 64MHz uart clock and it does not detect the
# serial device by number. Use the IO port directly and divide the baud down to
# trick syslinux into configuring a 115200 baud.
SYSLINUX_OPTS = "serial 0xb060 3318"
SERIAL_CONSOLE = "115200 ttyPCH1"
APPEND += "console=ttyPCH1,115200 console=tty0"

# EFI boot will ignore this and use the EFI framebuffer at 800x600
APPEND += "video=vesafb vga=0x318"

