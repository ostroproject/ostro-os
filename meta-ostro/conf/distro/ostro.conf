DISTRO = "ostro"
DISTRO_NAME = "Ostro OS"
# The version is bumped after each official release.
# This implies that new releases go out with a version
# string based on the predecessor plus the suffix, which
# was deemed sufficient in the "[Ostro-dev] versioning Ostro OS"
# mail thread.
DISTRO_VERSION = "1.0+snapshot-${DATE}"
DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VENDOR = "-ostrosdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"
SDK_NAME_PREFIX = "ostro"
SDK_UPDATE_URL = "${OSTRO_SDK_UPDATE_URL}"

# SDK_UPDATE_URL defines the eSDK updateserver configured in
# devtool.conf when populate-sdk-ext is run.
#
# Example SDK_UPDATE_URLs are:
# https://download.ostroproject.org/builds/ostro-os/latest/sdk-data/<machine> OR
# https://download.ostroproject.org/releases/ostro-os/milestone/<release>/sdk-data/<machine>
#
# The default setting can be overridded in local.conf
OSTRO_SDK_UPDATE_URL ??= "https://download.ostroproject.org/builds/ostro-os/latest/sdk-data/${MACHINE}"

# Extensible SDK configuration: Ostro uses "minimal" type which minimizes the size of the downloaded
# SDK itself and configures devtool to use SSTATE_MIRROR (defined in o sdk-extra.conf).
# The package data available gets recorded. This has the side-effect that exporting the eSDK
# for a certain image will not only cover the image, but also all packages that are included
# in a world build, and for doing that it will build all these packages.
SDK_EXT_TYPE = "minimal"
SDK_INCLUDE_PKGDATA = "1"

# Set custom tasks from Ostro layers as recursive dependencies to do_populate_sdk.
SDK_RECRDEP_TASKS = "do_deploy_files do_uefiapp"

# Blacklist inherited classes that break proper data restore from SSTATE when populating the SDK.
SDK_INHERIT_BLACKLIST = "buildhistory icecc buildhistory-extra buildstats-summary archiver isafw test-iot"

# The variable BB_NUMBER_PARSE_THREADS set in CI in order to
# limit the number of parse threads should not leak to the user's
# environment since it may increase SDK installation time and
# worsen runtime performance on capable systems.
SDK_LOCAL_CONF_BLACKLIST_append = " BB_NUMBER_PARSE_THREADS"

# Disable SSTATE locked sigs checks. SDK_INHERIT_BLACKLISTed classes
# used in CI (and mirrored SSTATE) cause locked signature file check
# failures with eSDK installation. Until the blacklisted classes are
# fixed, SSTATE checks need to be run in self-tests.
SIGGEN_LOCKEDSIGS_TASKSIG_CHECK = "none"

INHERIT += "ostro-version"

# Ostro OS tries to build minimal images and thus prefers Busybox or
# Toybox over coreutils or any other package providing similar
# functionality. For technical reasons, this is crucial when building
# swupd bundles: Busybox/Toybox must be picked consistently, even when
# a bundle pulls in alternatives like coreutils which normally would
# have a higher priority.
#
# This gets achieved in two ways:
# 1. via ostro-update-alternatives.bbclass we modify all packages
#    so that their update-alternatives priority can be modified
#    during install time
# 2. in ostro-image.bbclass we reconfigure those priorities
#    as necessary
#
# This is still a change that is done consistently for all images,
# whether they use swupd or not, to ensure that the result is
# identical. Doing it at image creation time reduces the need
# for recompilations of packages and opens up the possibility
# for downstream developers to override the defaults while
# reusing pre-compiled binaries from our sstate.
INHERIT += "ostro-update-alternatives"

MAINTAINER = "Ostro Dev <ostro-dev@lists.ostroproject.org>"

TARGET_VENDOR = "-ostro"

OSTRO_DEFAULT_DISTRO_FEATURES = "systemd bluez5 pam"
OSTRO_DEFAULT_EXTRA_RDEPENDS ??= ""
OSTRO_DEFAULT_EXTRA_RRECOMMENDS ??= ""

# Smack security is a distribution feature which can be enabled or
# disabled as needed. To simplify recipes, there is also a smack
# override.
#
# At the moment, these settings must be kept in sync manually: the
# "smack" override must be set if and only if the "smack" distro
# feature is set.
#
# In other words, to disable the default in a local.conf, change two variables
# like this:
#  OSTRO_SMACK_OVERRIDE = ""
#  OSTRO_DEFAULT_DISTRO_FEATURES_remove = "smack"

OSTRO_DEFAULT_DISTRO_FEATURES += "smack"
OSTRO_SMACK_OVERRIDE ?= ":smack"
DISTROOVERRIDES .= "${OSTRO_SMACK_OVERRIDE}"

# IMA/EVM depend on compiling some components (currently the kernel)
# differently. This distro feature enables that.
OSTRO_DEFAULT_DISTRO_FEATURES += "ima"

# ptests are useful for QA. Enabling them in the distro creates
# additional packages in each recipe that supports ptests and thus
# allows building images with those tests included. Enabling ptests
# should have no effect on normal packages; if there is an effect,
# then that is a bug in the recipe.
OSTRO_DEFAULT_DISTRO_FEATURES += "ptest"

# enable Java as distro feature - this adds the java-jdk package group into
# images and enables Java bindings and other Java dependent features
OSTRO_DEFAULT_DISTRO_FEATURES += "java"

# Remove currently unsupported distro features from global defaults
DISTRO_FEATURES_DEFAULT_remove = "x11 3g"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${DISTRO_FEATURES_LIBC} ${OSTRO_DEFAULT_DISTRO_FEATURES}"

# Currently Ostro OS is still using swupd 2.x because that is what
# has been tested with most. The newer 3.x will be tested right after
# the initial swupd integration.
PREFERRED_VERSION_swupd-client ?= "2.%"
PREFERRED_VERSION_swupd-client-native ?= "2.%"
PREFERRED_VERSION_swupd-server ?= "2.%"
PREFERRED_VERSION_swupd-server-native ?= "2.%"

# Use 4.4 kernel for meta-intel BSP MACHINEs
# BeagleBone is still on 4.1 until meta-yocto-bsp switches it over
PREFERRED_VERSION_linux-yocto_intel-corei7-64 ?= "4.4%"
PREFERRED_VERSION_linux-yocto_intel-quark ?= "4.4%"
PREFERRED_VERSION_linux-yocto_beaglebone ?= "4.1%"

# Use UEFI-based "dsk" image format for machines supporting UEFI.
# Defined here because it influences several different recipes.
OSTRO_USE_DSK_IMAGES ?= "${@bb.utils.contains_any('MACHINE', 'intel-core2-32 intel-corei7-64 intel-quark', 'True', 'False', d)}"

DISTRO_EXTRA_RDEPENDS += " ${OSTRO_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += " ${OSTRO_DEFAULT_EXTRA_RRECOMMENDS}"

QEMU_TARGETS ?= "arm i386 x86_64"

# Set PREMIRRORS to prioritize Ostro OS autobuilder source cache.
# The OSTRO_SOURCE_MIRROR_URL can be overriden in local.conf.
OSTRO_SOURCE_MIRROR_URL ??= "https://download.ostroproject.org/mirror/sources/"
SOURCE_MIRROR_URL = "${OSTRO_SOURCE_MIRROR_URL}"
INHERIT += "own-mirrors"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit pulseaudio"

require conf/distro/include/no-static-libs.inc
require conf/distro/include/ostro_security_flags.inc

# Build distro stateless, i.e. with /etc empty straight out of the box
# and reserved for customizations by the admin.
require conf/distro/include/stateless.inc

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths staticdev \
                    useless-rpaths build-deps"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

# Add some Ostro OS specific checks in addition to those provided by sanity.bbclass.
INHERIT += "ostro-sanity"

# Only specific recipes are supported by the Ostro Project. Those
# recipes are listed in the following file.
#
# It is possible to add additional file names, for example in derived
# distros or products, separated by spaces, or completely replace the
# default list.
SUPPORTED_RECIPES ?= " \
    ${META_OSTRO_BASE}/conf/distro/include/devtool-supported-recipes.txt \
    ${META_OSTRO_BASE}/conf/distro/include/ostro-supported-recipes.txt \
"
SUPPORTED_RECIPES[devtool-supported-recipes.txt] = "developer"
SUPPORTED_RECIPES[ostro-supported-recipes.txt] = "ostro"

# Inheriting this class ensures that a "world build" of Ostro OS only
# builds the supported recipes. In addition, local.conf.sample activates
# a check that ensures that nothing gets built which is not supported.
INHERIT += "supported-recipes"

# In some (rare) cases there are name clashes between layers and we would end up
# using an undesired version of a recipe. The supported-recipes.bbclass detects
# when a recipe comes from a different layer than expected. When that happens,
# we have to ignore the undesired one. Could be done with blacklisting, but
# BBMASKing it is easier. If a derived distro does not agree with the Ostro
# choice, they can unset OSTRO_BBMASK.
OSTRO_BBMASK = " \
    /meta-oe/recipes-support/libmicrohttpd/ \
"
BBMASK += "${OSTRO_BBMASK}"

# However, not all supported recipes also need to be part of a world
# build. It is enough that they can be built if needed for a specific
# image. Those recipes we remove from world explicitly.
EXCLUDE_FROM_WORLD_pn-u-boot = "1"
EXCLUDE_FROM_WORLD_pn-u-boot-edison = "1"
EXCLUDE_FROM_WORLD_pn-u-boot-fw-utils-edison = "1"
EXCLUDE_FROM_WORLD_pn-initramfs-boot = "1"

# Nothing prevents building something (like a custom image, for
# example) also with other recipes included in a build. However,
# sometimes that is unintentional or even an error, for example when
# some unexpected component gets pulled in via an unanticipated
# dependency change.
#
# To enable a warning about this before the build starts, set this
# variable to "warn" in local.conf. "fatal" prints the same message
# and aborts the build automatically.
#
# SUPPORTED_RECIPES_CHECK = "warn"

# Java bootstrapping
PREFERRED_PROVIDER_virtual/java-initial-native = "cacao-initial-native"
PREFERRED_PROVIDER_virtual/java-native = "cacao-native"
PREFERRED_PROVIDER_virtual/javac-native = "ecj-bootstrap-native"

# Java runtime: used by packagegroup-java-jdk.bb and packages which
# RDEPEND on Java.
#
# Must use openjdk-8 because Ostro OS is meant to support on-device
# compilation of Java programs, and we don't want to end up
# compiling and installing both jre and jdk (they are separate
# packages which share no files).
PREFERRED_RPROVIDER_java2-runtime = "openjdk-8"

# OE-core recently switched to gcc 6.1 (OE-core commit c20d863da5700, see tcmode-default.inc).
# However, that breaks compilation of iotivity 1.1.0 (IOTOS-1625, "call of overloaded 'abs(double)' is ambiguous")
# and linux-yocto-edison 3.10.98 (IOTOS-1626, "fatal error: linux/compiler-gcc6.h: No such file or directory").
#
# Until these issues are fixed, we need to stay on gcc 5.x, the previous default.
GCCVERSION ?= "5.%"

# Compiling openjdk-8 has some support for compiling with gcc 6.x, but that
# support passes -fno-lifetime-dse also to the host compiler, causing
# openjdk-8-native do_configure to fail ("cannot produce executables") and
# in openjdk-8 do_compile to fail on some helper tools. Problem reported
# upstream, see http://thread.gmane.org/gmane.comp.handhelds.openembedded/75469
# In the meantime we just remove the offending (and unneeded, while we
# are on gcc 5.x) flag:
CFLAGS_remove_pn-openjdk-8-native = "-fno-lifetime-dse"
CFLAGS_remove_pn-openjdk-8 = "-fno-lifetime-dse"
CFLAGS_remove_pn-openjre-8-native = "-fno-lifetime-dse"
CFLAGS_remove_pn-openjre-8 = "-fno-lifetime-dse"
CXXFLAGS_remove_pn-openjdk-8-native = "-fno-lifetime-dse"
CXXFLAGS_remove_pn-openjdk-8 = "-fno-lifetime-dse"
CXXFLAGS_remove_pn-openjre-8-native = "-fno-lifetime-dse"
CXXFLAGS_remove_pn-openjre-8 = "-fno-lifetime-dse"

# Ostro OS removes certain packages from images because the components are known
# to have compile issues and/or are not supported. Can be modified in derived
# distros or via local.conf.
OSTRO_OS_REMOVE_PACKAGES ?= " \
    oprofileui-server \
    lttng-tools \
"
RDEPENDS_packagegroup-core-device-devel_remove_pn-packagegroup-core-device-devel = "${OSTRO_OS_REMOVE_PACKAGES}"
RDEPENDS_packagegroup-core-tools-profile_remove_pn-packagegroup-core-tools-profile = "${OSTRO_OS_REMOVE_PACKAGES}"

# It is uncertain whether rhino is really an allarch package. It's signature
# is different among machines, causing the iotsstatetests.SStateTests.test_sstate_samesigs
# to fail. We play it save here and make the recipe specific to the tune flags.
# Needs further investigations by upstream.
PACKAGE_ARCH_pn-rhino = "${TUNE_PKGARCH}"

# Set a proper filter to classify internal vs. external patches (only internal
# patches are of interest) in the patch summary created by inputanalyzer.
INPUTANALYZER_WHITELIST = '/(meta|meta-yocto-bsp|meta-intel|meta-java|meta-oic|meta-oe|meta-networking|meta-python|meta-intel-iot-middleware|meta-security-isafw)/*$ ^$'

# re-use uninative shim released by Yocto Project / OE
require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative"

# Disable running fsck at boot. System clock is typically wrong at early boot
# stage due to lack of RTC backup battery. This causes unnecessary fixes being
# made due to filesystem metadata time stamps being in future.
APPEND_append = " fsck.mode=skip"
